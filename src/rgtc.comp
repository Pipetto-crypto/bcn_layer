#version 450
/* Copyright (c) 2020-2024 Hans-Kristian Arntzen
 *
 * Permission is hereby granted, free of charge, to any person obtaining
 * a copy of this software and associated documentation files (the
 * "Software"), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to
 * permit persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

#include "rgtc.h"

#define VK_FORMAT_BC4_UNORM_BLOCK 139
#define VK_FORMAT_BC4_SNORM_BLOCK 140
#define VK_FORMAT_BC5_UNORM_BLOCK 141
#define VK_FORMAT_BC5_SNORM_BLOCK 142

layout(set = 0, binding = 0) writeonly buffer uOutputBlock {
	uint[] data;
} uOutput;

layout(set = 0, binding = 1) readonly buffer uInputBlock {
	uint[] data;
} uInput;

layout(push_constant) uniform Registers
{
	int format;
	int width;
	int height;
	int offset;
	int bufferRowLength;
	int offsetX;
	int offsetY;
} registers;

void main()
{
    int format = registers.format;
    int width = registers.width;
    int height = registers.height;
    int offset = registers.offset;
    int bufferRowLength = registers.bufferRowLength;
    ivec2 resolution = ivec2(width, height);

    int x = int(gl_WorkGroupID.x * 8 + gl_LocalInvocationID.x);
	int y = int(gl_WorkGroupID.y * 8 + gl_LocalInvocationID.y);
    ivec2 coord = ivec2(x, y);

    bool is_snorm = (format == VK_FORMAT_BC4_SNORM_BLOCK || format == VK_FORMAT_BC5_SNORM_BLOCK);
    
    if (any(greaterThanEqual(coord, resolution)))
        return;

    ivec2 tile_coord = coord / 4;
    ivec2 pixel_coord = coord % 4;

    int rowExtent = max(bufferRowLength, width);
    int blocks_per_row = (rowExtent + 3) / 4;
    int block_index = tile_coord.y * blocks_per_row + tile_coord.x;
    int bc_words = (format == VK_FORMAT_BC4_UNORM_BLOCK || format == VK_FORMAT_BC4_SNORM_BLOCK) ? 2 : 4;
    int block_offset = bc_words * block_index;
    uvec4 payload = uvec4(uInput.data[block_offset],
    					  uInput.data[block_offset + 1],
    					  (bc_words > 2) ? uInput.data[block_offset + 2] : 0u,
    					  (bc_words > 2) ? uInput.data[block_offset + 3] : 0u);
    
    int linear_pixel = 4 * pixel_coord.y + pixel_coord.x;

    vec4 rg = vec4(0);

    rg.x = decode_alpha_rgtc(payload.xy, linear_pixel);

    if (format == VK_FORMAT_BC5_UNORM_BLOCK || format == VK_FORMAT_BC5_SNORM_BLOCK)
        rg.y = decode_alpha_rgtc(payload.zw, linear_pixel);
    else
    	rg.y = 0;

    rg.z = 0;
    rg.w = 1.0;

    int pixel_index = coord.y * width + coord.x;
    uOutput.data[pixel_index] = packUnorm4x8(rg);
}
